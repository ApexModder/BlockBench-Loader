plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
}

loom {
    if(accessWidenerFile.exists()) {
        accessWidenerPath = accessWidenerFile
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${MINECRAFT_VERSION}"

    mappings(loom.layered {
        officialMojangMappings()

        if(rootProject.hasProperty('PARCHMENT_MAPPINGS')) {
            def mcVersion = rootProject.hasProperty('PARCHMENT_MINECRAFT_VERSION') ? "${PARCHMENT_MINECRAFT_VERSION}" : "${MINECRAFT_VERSION}"
            parchment("org.parchmentmc.data:parchment-${mcVersion}:${PARCHMENT_MAPPINGS}@zip")
        }
    })

    modImplementation "net.fabricmc:fabric-loader:${FABRIC_LOADER_VERSION}"
    modApi "net.fabricmc.fabric-api:fabric-api:${FABRIC_API_VERSION}+${MINECRAFT_VERSION}"

    compileOnly project(':common')
    implementation project(path: ':common', configuration: 'namedElements')

    compileOnly project(':testmod-common')
    implementation project(path: ':testmod-common', configuration: 'namedElements')
}

processResources {
    from project(':testmod-common').sourceSets.main.resources
}

compileJava {
    source project(':testmod-common').sourceSets.main.allSource
}
